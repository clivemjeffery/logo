to boxoutline :xmin :xmax :ymin :ymax
  pu
  setxy :xmin :ymin
  seth 0
  pd
  repeat 2 [
    fd :ymax - :ymin rt 90
    fd :xmax - :xmin rt 90
  ]
  pu
  home
  pd
end

to goldenmond
; http://www.tate.org.uk/context-comment/video/tateshots-piet-mondrian
  localmake "xmin item 1 activearea
  localmake "xmax item 3 activearea
  localmake "yd (item 4 activearea) - (item 2 activearea)
  localmake "ygr :yd / 1.618 ; golden ratio
  localmake "ymin (item 2 activearea) + (:yd - :ygr)/2
  localmake "ymax :ymin + :ygr
  ;show :xmin show :xmax show :yd show :ygr show :ymin show :ymax
  cs
  setpensize 8
  boxoutline :xmin :xmax :ymin :ymax
  repeat 3 [ ; vertical lines
     pu
     setxy :xmin :ymin
     pd
     seth 90 fd random :xmax - :xmin
     seth 0 fd :ymax - :ymin
  ]
  repeat 3 [ ; horizontal lines
     pu
     setxy :xmin :ymin
     pd
     seth 0 fd random int :ymax - :ymin
     seth 90 fd :xmax - :xmin
  ]
  ; colour
  pu
  repeat 16 [
    setxy :xmin :ymin
    seth 90 fd random :xmax - :xmin
    seth 0 fd random int :ymax - :ymin
    if (pixel = screencolour) [
      setfloodcolour pick [blue yellow red green]
      fill
    ]
  ]
  pd
end

